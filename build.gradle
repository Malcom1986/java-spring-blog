plugins {
  id 'org.springframework.boot' version '2.4.3'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'

  id 'com.adarshr.test-logger' version '2.1.1'
  id 'io.freefair.lombok' version '5.3.0'
  id 'org.liquibase.gradle' version '2.0.4'
  // TODO: https://github.com/spotbugs/spotbugs-gradle-plugin/issues/32
  // id 'com.github.spotbugs' version '4.6.2'
  id 'checkstyle'
  id 'com.github.ben-manes.versions' version '0.38.0'
 	// id 'net.ltgt.errorprone' version '1.3.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

ext {
  checkstyleVersion = '8.41'
}

configurations {
  checkstyleConfig
}

afterEvaluate {
  checkstyle {
    toolVersion project.ext.checkstyleVersion
    // https://github.com/square/okhttp/pull/5720/files
    config resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
  }
}

dependencies {
  checkstyleConfig dependencies.create("com.puppycrawl.tools:checkstyle:${project.ext.checkstyleVersion}") {
    transitive = false
  }

  developmentOnly 'org.springframework.boot:spring-boot-devtools'
  implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

  // https://mvnrepository.com/artifact/com.github.database-rider/rider-spring
  implementation group: 'com.tobedevoured.modelcitizen', name: 'spring', version: '0.8.1'

  testImplementation 'org.springframework.boot:spring-boot-starter-test'

  runtimeOnly 'com.h2database:h2'

  liquibaseRuntime 'org.liquibase:liquibase-core'
  liquibaseRuntime 'org.liquibase.ext:liquibase-hibernate5:3.6'
  liquibaseRuntime 'org.springframework.boot:spring-boot'
  liquibaseRuntime 'org.springframework:spring-orm'
  liquibaseRuntime 'ch.qos.logback:logback-classic:1.2.3' // Fix for https://liquibase.jira.com/browse/CORE-3212
  liquibaseRuntime 'javax.xml.bind:jaxb-api'

  liquibaseRuntime sourceSets.main.output
}

diff.dependsOn compileJava
diffChangeLog.dependsOn compileJava

test {
	useJUnitPlatform()
}

liquibase {
  activities {
    main {
      // TODO Теперь разобраться как убрать дублирование и вынести переменные в конфиг приложения
      classpath "$projectDir/src/main/resources/db/changelog"
      // NOTE в ямле не работает генерация changeset
      changeLogFile "${projectDir}/src/main/resources/db/changelog/db.changelog-master.xml"
      url 'jdbc:h2:file:./development'
      driver 'org.h2.Driver'// System.getProperty('spring.datasource.driver-class-name')
      username 'tirion'
      password 'password'

      // referenceDriver 'org.h2.Driver'
      referenceUrl 'hibernate:spring:com.example.javaspringblog.models?dialect=org.hibernate.dialect.H2Dialect'
    }
  }
}
